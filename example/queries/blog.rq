PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX schema: <https://schema.org/>

INSERT {
    # Entry to the blog itself
    ?blog schema:blogPost ?post .

    # Short summary for the blog index page
    ?summary schema:title ?title ;
        schema:abstract ?abstract ;
        schema:datePublished ?published ;
        schema:url ?post ;
        schema:keywords ?keyword .
} WHERE {
    ?blog rdf:type schema:Blog .

    ?post rdf:type schema:BlogPosting ;
        schema:title ?title ;
        schema:abstract ?abstract ;
        schema:datePublished ?published ;
        schema:keywords ?keyword .

    BIND(URI(CONCAT(STR(?blog), "#", MD5(STR(?post)))) AS ?summary)
}
